// Date Constants
let reportdate = toscalar(ExportAuditLogRollupV3 | summarize max(startofday(SQLizerStreamDateId))); // Use latest snapshot
//let reportdate = datetime(2018-07-09); // Use this to use a different snapshot date
let startdate = datetime_add('day', -45, reportdate);
let enddate = datetime_add('day', -30, reportdate);
//
// Intentionally exclude specific agents
let excludedAgents = datatable(AgentId:string)
[
];
//
let allowedAgents = materialize
( 
    cluster("Ngpreporting").database('NgpInfo').fcnDataAgentOwnerInfo()
    | where AgentReadiness == "ProdReady"
    | summarize by AgentId
    | join kind=leftantisemi excludedAgents on AgentId
);
// Info on production agents
let AgentOwnerInfo = materialize
(
    cluster("Ngpreporting").database('NgpInfo').fcnDataAgentOwnerInfo()
    | where AgentReadiness == "ProdReady" // Filters to only ProdReady agents
    | project AgentId, DivisionName, OrganizationName, DivisionOwner=tostring(split(DivisionOwners, ";")[0]), OrganizationOwner=tostring(split(OrganizationOwners, ";")[0]), GC, Email, Protocol, AliasDict=toscalar(cluster("Ngpreporting").database('NgpInfo').GetNameByAlias())
    | project AgentId, DivisionName, OrganizationName, DivisionOwner, GC, Email, Protocol, DivLookup=AliasDict[DivisionOwner], OrganizationOwner, OrgLookup=AliasDict[OrganizationOwner]
    | project AgentId, DivisionName=iif(DivisionName == "", "Unknown", DivisionName), OrganizationName=iif(OrganizationName == "", "Unknown", OrganizationName), GC, Email, Protocol, DivisionVP=iif(DivLookup != "", DivLookup, DivisionOwner),OrganizationLeader=iif(OrgLookup != "", OrgLookup, OrganizationOwner)
); 
// Agent Level information
let AgentSummary = materialize
(
    ExportAuditLogRollupV3
    | where CompleteType != "NotApplicable"
    | where SQLizerStreamDateId == reportdate
    | where startofday(ExportRequestDate) >= startdate and startofday(ExportRequestDate) < enddate
    | where AgentId in (allowedAgents)
    | summarize 
        Uncompleted=countif((isnull(CompletedTime) or tolower(CompleteType) == 'auto'))
        by AgentId, CommandId, AssetGroupId
    | summarize 
        CountTotal=count(),
        CountMissing=sum(Uncompleted)
        by AgentId
);
// Convert into standard names for report rollup
let AgentLevelData = materialize
(
    AgentSummary
    | project 
        AgentId,
        Acknowledged = CountTotal - CountMissing,
        Pending = CountMissing
    | join kind=rightouter AgentOwnerInfo on AgentId
);
// Total rollup
let Total = materialize
(
    AgentLevelData
    | summarize 
        Acknowledged=sum(Acknowledged), 
        Total=sum(Acknowledged + Pending), 
        AgentCount=countif(AgentId != "")
    | project 
        DisplayName = "Total", 
        SortName = "z", 
        AgentCount, 
        Acknowledged, 
        Total, 
        PostSLACompletionStatus=iif(Total > 0, toreal(Acknowledged) / toreal(Total), toreal(0))
);
// Division rollup
let ByDivision = materialize
(
    AgentLevelData
    | summarize 
        Acknowledged=sum(Acknowledged), 
        Total=sum(Acknowledged + Pending), 
        AgentCount=countif(AgentId != "") 
        by DivisionName, DivisionVP
    | project 
        DisplayName = DivisionName, 
        SortName = strcat(DivisionName, "."), 
        AgentCount, 
        Acknowledged, 
        Total, 
        PostSLACompletionStatus=iif(Total > 0, toreal(Acknowledged) / toreal(Total), toreal(0)), 
        DivisionVP
);
// Org rollup
let ByOrg = materialize
(   
    AgentLevelData
    | summarize 
        Acknowledged=sum(Acknowledged), 
        Total=sum(Acknowledged + Pending), 
        AgentCount=countif(AgentId != "") 
        by DivisionName, OrganizationName, GC, Email, OrganizationLeader
    | where Total > 0
    | project 
        DisplayName = strcat("    ", OrganizationName), 
        SortName = strcat(DivisionName, ".", OrganizationName), 
        AgentCount, 
        Acknowledged, 
        Total, 
        PostSLACompletionStatus=iif(Total > 0, toreal(Acknowledged) / toreal(Total), toreal(0)), 
        GC, 
        Email, 
        OrganizationLeader
    | where PostSLACompletionStatus < 0.999
);
// Report summary
Total
| union ByDivision 
| union ByOrg 
| order by SortName asc
| project 
    DisplayName, 
    AgentCount, 
    Counts=strcat(Acknowledged," / ", Total), 
    PostSLACompletionStatus=strcat(round(PostSLACompletionStatus*100, 2), "%"), 
    GC, 
    GCEmail=Email, 
    DivisionVP, 
    OrganizationLeader
